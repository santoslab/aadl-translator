property set MAP_Error_Properties is
with EMV2, MAP_Errors;

-- ====================================== --
-- Types related to the report background --
-- ====================================== --

Context : type aadlstring;

Assumption : type aadlstring;

Abbreviation : type record (
	Full : aadlstring;
	Definition : aadlstring;
);

-- =================================== --
-- Types related to STPA preliminaries --
-- =================================== --

Accident_Level : type record (
	Level : aadlinteger;
	Description: aadlstring;
);

Accident : type record (
	Number : aadlinteger;
	Description : aadlstring;
	Level : MAP_Error_Properties::Accident_Level; 
);

Hazard: type record (
	Number : aadlinteger;
	Description : aadlstring;
	Accident : MAP_Error_Properties::Accident;
);

Constraint: type record (
	Number : aadlinteger;
	Description : aadlstring;
	Hazard : MAP_Error_Properties::Hazard;	
);

-- ============================ --
-- Types related to STPA step 1 --
-- ============================ --

KindsType : type enumeration (
	NotProviding, Providing, Early, Late, AppliedTooLong, StoppedTooSoon, 
	ValueLow, ValueHigh, ParamsMissing, ParamsWrong, ParamsOutOfOrder
);

Occurrence : record (
	Kind : MAP_Error_Properties::KindsType;
	Hazard : MAP_Error_Properties::Hazard;
	ViolatedConstraint : MAP_Error_Properties::Constraint;
	Description : aadlstring;
	Cause : aadlstring;
	Compensation : aadlstring;
	Impact : reference ({emv2}**error type);
) applies to ({emv2}**connection error source);

end MAP_Error_Properties;