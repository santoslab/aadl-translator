property set MAP_Error_Properties is
with EMV2, MAP_Errors;

-- ====================================== --
-- Types related to the report background --
-- ====================================== --

Context : type aadlstring;

Assumption : type aadlstring;

Abbreviation : type record (
	Full : aadlstring;
	Definition : aadlstring;
);

-- =================================== --
-- Types related to STPA preliminaries --
-- =================================== --

Accident_Level : type record (
	Level : aadlinteger;
	Description: aadlstring;
	Explanations : list of aadlstring;
	Accidents : list of MAP_Error_Properties::Accident;
);

Accident : type record (
	Description : aadlstring;
	Explanations : list of aadlstring;
	Hazards : list of MAP_Error_Properties::Hazard;
);

Hazard: type record (
	Name : aadlstring;
	Description : aadlstring;
	HazardousFactor : aadlstring;
	SystemElement : reference (device, process);
	SystemElementState : reference({emv2}** error type, {emv2}** error type set);
	EnvironmentElement : reference (abstract);
	EnvironmentElementStates : list of reference({emv2}** error type);
	Explanations : list of aadlstring;
	Constraints : list of MAP_Error_Properties::Constraint;
);

Constraint: type record (
	Name : aadlstring;
	Description : aadlstring;
	ErrorType : reference({emv2}** error type);
);

Fundamentals : list of MAP_Error_Properties::Accident_Level applies to (all);

ProcessVariable : type record (
	Name : aadlstring;
	Values : list of reference(mode);	
);

ProcessModel : list of MAP_Error_Properties::ProcessVariable applies to (all);

ExternallyCausedDanger : record (
	ControlledProcessState : reference(mode);
	ProcessVariableValue : reference(mode);
	Explanation : aadlstring;
) applies to (all);

--Compensation : type aadlstring;

ErrorHandlingApproachType : type enumeration (Rollback, Rollforward, Compensation);

-- Partially tested, no DesignTimeHandling or ControlledProcessState coverage
InternallyCausedDanger : record (
	ControlledProcessState : reference({emv2}** error type);
--	DesignTimeHandling : list of MAP_Error_Properties::Compensation;
	Explanation : aadlstring;
) applies to (all);

-- Tested
RuntimeErrorDetection : record (
	ErrorDetectionApproach : enumeration (Concurrent, Preemptive);
	Explanation : aadlstring; 
) applies to (all);

-- Tested
RuntimeErrorHandling : record (
	ErrorHandlingApproach : MAP_Error_Properties::ErrorHandlingApproachType;
	Explanation : aadlstring;
) applies to (all);

DesignTimeFaultDetection : record (
	FaultDetectionApproach : enumeration (StaticAnalysis, TheoremProving, ModelChecking, SymbolicExecution, Testing);
	Explanation : aadlstring;
) applies to (all);

RuntimeFaultHandling : record (
	FaultHandlingApproach : enumeration (Diagnosis, Isolation, Reconfiguration, Reinitialization);
	ErrorHandlingApproach : MAP_Error_Properties::ErrorHandlingApproachType;
	Explanation : aadlstring;
) applies to (all);

-- Tested
EliminatedFaults : record (
	FaultTypes : list of reference({emv2}** error type);
	Explanation : aadlstring;	
) applies to (all);

end MAP_Error_Properties;